# run: codegen

func @iota(ptr, i64) {
    # check: function `iota`:
    # nextln: 000000: 55                              push rbp
    # nextln: 000001: 48 89 e5                        mov rbp, rsp
    # nextln: 000004: 33 c0                           xor eax, eax
    # nextln: 000006: 48 85 f6                        test rsi, rsi
    # nextln: 000009: 0f 84 2c 00 00 00               je 0x3b
    # nextln: 00000f: b9 03 00 00 00                  mov ecx, 3
    # nextln: 000014: 48 89 c2                        mov rdx, rax
    # nextln: 000017: 48 d3 e2                        shl rdx, cl
    # nextln: 00001a: 49 89 f8                        mov r8, rdi
    # nextln: 00001d: 4c 03 c2                        add r8, rdx
    # nextln: 000020: 48 89 c2                        mov rdx, rax
    # nextln: 000023: 48 83 c2 01                     add rdx, 1
    # nextln: 000027: 49 89 00                        mov qword ptr [r8], rax
    # nextln: 00002a: 48 3b d6                        cmp rdx, rsi
    # nextln: 00002d: 0f 84 08 00 00 00               je 0x3b
    # nextln: 000033: 48 89 d0                        mov rax, rdx
    # nextln: 000036: e9 d9 ff ff ff                  jmp 0x14
    # nextln: 00003b: 5d                              pop rbp
    # nextln: 00003c: c3                              ret

    %entry_ctrl:ctrl, %arr:ptr, %n:i64 = entry
    %zero:i64 = iconst 0
    %zerocmp:i32 = icmp eq %n, %zero
    %iszero:ctrl, %isnonzero:ctrl = brcond %entry_ctrl, %zerocmp
    %loopbody:ctrl, %loopphi:phisel = region %isnonzero, %looplatch
    %i:i64 = phi %loopphi, %zero, %inext
    %three:i64 = iconst 3
    %off:i64 = shl %i, %three
    %ptr:ptr = ptroff %arr, %off
    %poststore:ctrl = store.8 %loopbody, %i, %ptr
    %one:i64 = iconst 1
    %inext:i64 = iadd %i, %one
    %donecmp:i32 = icmp eq %inext, %n
    %loopdone:ctrl, %looplatch:ctrl = brcond %poststore, %donecmp
    %exit:ctrl, %exitphi:phisel = region %iszero, %loopdone
    return %exit
}
