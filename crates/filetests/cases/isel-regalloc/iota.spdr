# run: isel-regalloc

func @iota(ptr, i64) {
    # check: function `iota`:
    # nextln: clobbers: rax, rcx, rdx
    # nextln: frame: size 0, align 0
    # nextln:       block0:
    # nextln: 0000:     $$rax = MovRmS32(0)
    # nextln: 0001:     AluRRm(S64, Test) $$rsi, $$rsi
    # nextln: 0002:     Jumpcc(E, block1, block3)
    # nextln:       block1:
    # nextln: 0003:     Jump(block2)
    # nextln:       block3:
    # nextln: 0004:     Jump(block4)
    # nextln:       block4:
    # nextln:           $$rdx:gpr = $$rax
    # nextln: 0005:     $$rdx = ShiftRmI(S64, Shl, 3) $$rdx
    # nextln: 0006:     $$rdx = AddRR(S64) $$rdi, $$rdx
    # nextln: 0007:     $$rcx = AddRI(S64, 1) $$rax
    # nextln: 0008:     MovMR(S64, [reg + 0]) $$rax, $$rdx
    # nextln: 0009:     AluRRm(S64, Cmp) $$rcx, $$rsi
    # nextln: 0010:     Jumpcc(E, block5, block6)
    # nextln:       block6:
    # nextln:           $$rax:gpr = $$rcx
    # nextln: 0011:     Jump(block4)
    # nextln:       block5:
    # nextln: 0012:     Jump(block2)
    # nextln:       block2:
    # nextln: 0013:     Ret

    %entry_ctrl:ctrl, %arr:ptr, %n:i64 = entry
    %zero:i64 = iconst 0
    %zerocmp:i32 = icmp eq %n, %zero
    %iszero:ctrl, %isnonzero:ctrl = brcond %entry_ctrl, %zerocmp
    %loopbody:ctrl, %loopphi:phisel = region %isnonzero, %looplatch
    %i:i64 = phi %loopphi, %zero, %inext
    %three:i64 = iconst 3
    %off:i64 = shl %i, %three
    %ptr:ptr = ptroff %arr, %off
    %poststore:ctrl = store.8 %loopbody, %i, %ptr
    %one:i64 = iconst 1
    %inext:i64 = iadd %i, %one
    %donecmp:i32 = icmp eq %inext, %n
    %loopdone:ctrl, %looplatch:ctrl = brcond %poststore, %donecmp
    %exit:ctrl, %exitphi:phisel = region %iszero, %loopdone
    return %exit
}
