# run: isel-regalloc[no-verify-regalloc]

func @sum_to_n:i32(i32) {
    # check: function `sum_to_n`:
    # nextln: clobbers: rax, rcx, rdx, rdi, rsi
    # nextln: frame: size 0, align 0
    # nextln:       block0:
    # nextln: 0000:     $$rax = MovRI(0)
    # nextln: 0001:     AluRRm(S32, Cmp) $$rdi, $$rax
    # nextln: 0002:     Jumpcc(E, block1, block3)
    # nextln:       block1:
    # nextln: 0003:     Jump(block2)
    # nextln:       block3:
    # nextln: 0004:     $$rcx = MovRI(1)
    # nextln:           $$rdx = $$rax
    # nextln: 0005:     Jump(block4)
    # nextln:       block4:
    # nextln:           $$rsi = $$rdi
    # nextln: 0006:     $$rsi = AluRRm(S32, Sub) $$rsi, $$rcx
    # nextln: 0007:     $$rdx = AluRRm(S32, Add) $$rdx, $$rdi
    # nextln: 0008:     AluRRm(S32, Cmp) $$rsi, $$rax
    # nextln: 0009:     Jumpcc(E, block5, block6)
    # nextln:       block6:
    # nextln:           $$rdi = $$rsi
    # nextln: 0010:     Jump(block4)
    # nextln:       block5:
    # nextln:           $$rax = $$rdx
    # nextln: 0011:     Jump(block2)
    # nextln:       block2:
    # nextln: 0012:     Ret $$rax

    %0:ctrl, %1:i32 = entry
    %10:i32 = iconst 1
    %2:i32 = iconst 0
    %3:i32 = icmp eq %1, %2
    %4:ctrl, %5:ctrl = brcond %0, %3
    %13:i32 = icmp eq %11, %2
    %14:ctrl, %15:ctrl = brcond %6, %13
    %16:ctrl, %17:phisel = region %4, %14
    %6:ctrl, %7:phisel = region %5, %15
    %8:i32 = phi %7, %1, %11
    %11:i32 = isub %8, %10
    %9:i32 = phi %7, %2, %12
    %12:i32 = iadd %9, %8
    %18:i32 = phi %17, %2, %12
    return %16, %18
}


func @sum_to_n_with_stack:i32(i32) {
    # check: function `sum_to_n_with_stack`:
    # nextln: clobbers: rax, rcx, rdx, rdi, rsi
    # nextln: frame: size 8, align 4
    # nextln:     !0: 0
    # nextln:     !1: 4
    # nextln:       block0:
    # nextln: 0000:     Jump(block1)
    # nextln:       block1:
    # nextln: 0001:     $$rax = MovRI(0)
    # nextln: 0002:     MovStackR(!0, S32) $$rdi
    # nextln: 0003:     MovStackR(!1, S32) $$rax
    # nextln: 0004:     $$rcx = MovRI(1)
    # nextln: 0005:     Jump(block2)
    # nextln:       block2:
    # nextln: 0006:     $$rdx = MovRStack(!0, S32)
    # nextln: 0007:     AluRRm(S32, Cmp) $$rdx, $$rax
    # nextln: 0008:     Jumpcc(E, block3, block4)
    # nextln:       block4:
    # nextln: 0009:     $$rsi = MovRStack(!0, S32)
    # nextln:           $$rdx = $$rsi
    # nextln: 0010:     $$rdx = AluRRm(S32, Sub) $$rdx, $$rcx
    # nextln: 0011:     $$rdi = MovRStack(!1, S32)
    # nextln: 0012:     $$rdi = AluRRm(S32, Add) $$rdi, $$rsi
    # nextln: 0013:     MovStackR(!0, S32) $$rdx
    # nextln: 0014:     MovStackR(!1, S32) $$rdi
    # nextln: 0015:     Jump(block2)
    # nextln:       block3:
    # nextln: 0016:     $$rax = MovRStack(!1, S32)
    # nextln: 0017:     Ret $$rax

    %0:ctrl, %1:i32 = entry
    %2:ctrl, %3:phisel = region %0
    %5:ptr = stackslot 4:4
    %4:ptr = stackslot 4:4
    %6:ctrl = store.4 %2, %1, %4
    %24:i32 = iconst 1
    %7:i32 = iconst 0
    %8:ctrl = store.4 %6, %7, %5
    %13:i32 = icmp eq %12, %7
    %18:ctrl, %19:ctrl = brcond %11, %13
    %14:ctrl, %15:phisel = region %18
    %29:ctrl, %30:i32 = load.4 %14, %5
    return %29, %30
    %16:ctrl, %17:phisel = region %19
    %20:ctrl, %21:i32 = load.4 %16, %4
    %22:ctrl, %23:i32 = load.4 %20, %5
    %25:i32 = isub %21, %24
    %27:ctrl = store.4 %22, %25, %4
    %26:i32 = iadd %23, %21
    %28:ctrl = store.4 %27, %26, %5
    %9:ctrl, %10:phisel = region %8, %28
    %11:ctrl, %12:i32 = load.4 %9, %4
}
