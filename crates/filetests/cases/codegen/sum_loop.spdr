# run: codegen

func @sum_to_n:i32(i32) {
    # check: function `sum_to_n`:
    # nextln: 000000: 55                              push rbp
    # nextln: 000001: 48 89 e5                        mov rbp, rsp
    # nextln: 000004: 33 c0                           xor eax, eax
    # nextln: 000006: 85 ff                           test edi, edi
    # nextln: 000008: 0f 84 1c 00 00 00               je 0x2a
    # nextln: 00000e: b9 01 00 00 00                  mov ecx, 1
    # nextln: 000013: 48 89 fa                        mov rdx, rdi
    # nextln: 000016: 2b d1                           sub edx, ecx
    # nextln: 000018: 03 c7                           add eax, edi
    # nextln: 00001a: 85 d2                           test edx, edx
    # nextln: 00001c: 0f 84 08 00 00 00               je 0x2a
    # nextln: 000022: 48 89 d7                        mov rdi, rdx
    # nextln: 000025: e9 e9 ff ff ff                  jmp 0x13
    # nextln: 00002a: 5d                              pop rbp
    # nextln: 00002b: c3                              ret

    %0:ctrl, %1:i32 = entry
    %10:i32 = iconst 1
    %2:i32 = iconst 0
    %3:i32 = icmp eq %1, %2
    %4:ctrl, %5:ctrl = brcond %0, %3
    %13:i32 = icmp eq %11, %2
    %14:ctrl, %15:ctrl = brcond %6, %13
    %16:ctrl, %17:phisel = region %4, %14
    %6:ctrl, %7:phisel = region %5, %15
    %8:i32 = phi %7, %1, %11
    %11:i32 = isub %8, %10
    %9:i32 = phi %7, %2, %12
    %12:i32 = iadd %9, %8
    %18:i32 = phi %17, %2, %12
    return %16, %18
}


func @sum_to_n_with_stack:i32(i32) {
    # check: function `sum_to_n_with_stack`:
    # nextln: 000000: 55                              push rbp
    # nextln: 000001: 48 89 e5                        mov rbp, rsp
    # nextln: 000004: 48 83 ec 10                     sub rsp, 0x10
    # nextln: 000008: 33 c0                           xor eax, eax
    # nextln: 00000a: 89 3c 24                        mov dword ptr [rsp], edi
    # nextln: 00000d: 89 44 24 04                     mov dword ptr [rsp + 4], eax
    # nextln: 000011: b8 01 00 00 00                  mov eax, 1
    # nextln: 000016: 8b 0c 24                        mov ecx, dword ptr [rsp]
    # nextln: 000019: 85 c9                           test ecx, ecx
    # nextln: 00001b: 0f 84 1a 00 00 00               je 0x3b
    # nextln: 000021: 8b 3c 24                        mov edi, dword ptr [rsp]
    # nextln: 000024: 48 89 f9                        mov rcx, rdi
    # nextln: 000027: 2b c8                           sub ecx, eax
    # nextln: 000029: 8b 54 24 04                     mov edx, dword ptr [rsp + 4]
    # nextln: 00002d: 03 d7                           add edx, edi
    # nextln: 00002f: 89 0c 24                        mov dword ptr [rsp], ecx
    # nextln: 000032: 89 54 24 04                     mov dword ptr [rsp + 4], edx
    # nextln: 000036: e9 db ff ff ff                  jmp 0x16
    # nextln: 00003b: 8b 44 24 04                     mov eax, dword ptr [rsp + 4]
    # nextln: 00003f: 48 83 c4 10                     add rsp, 0x10
    # nextln: 000043: 5d                              pop rbp
    # nextln: 000044: c3                              ret

    %0:ctrl, %1:i32 = entry
    %2:ctrl, %3:phisel = region %0
    %5:ptr = stackslot 4:4
    %4:ptr = stackslot 4:4
    %6:ctrl = store.4 %2, %1, %4
    %24:i32 = iconst 1
    %7:i32 = iconst 0
    %8:ctrl = store.4 %6, %7, %5
    %13:i32 = icmp eq %12, %7
    %18:ctrl, %19:ctrl = brcond %11, %13
    %14:ctrl, %15:phisel = region %18
    %29:ctrl, %30:i32 = load.4 %14, %5
    return %29, %30
    %16:ctrl, %17:phisel = region %19
    %20:ctrl, %21:i32 = load.4 %16, %4
    %22:ctrl, %23:i32 = load.4 %20, %5
    %25:i32 = isub %21, %24
    %27:ctrl = store.4 %22, %25, %4
    %26:i32 = iadd %23, %21
    %28:ctrl = store.4 %27, %26, %5
    %9:ctrl, %10:phisel = region %8, %28
    %11:ctrl, %12:i32 = load.4 %9, %4
}
