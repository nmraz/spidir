# run: isel

func @sum_to_n:i32(i32) {
    # check: function `sum_to_n`:
    # nextln: block0[%7:gpr($$rdi)]:
    # nextln:     %5:gpr(reg)[late] = MovRI(S32, 0)
    # nextln:     AluRRm(S32, Cmp) %7(reg)[early], %5(any)[early]
    # nextln:     Jumpcc(E, block1, block3)
    # nextln: => block1, block3
    # nextln: block1:
    # nextln:     Jump(block2)
    # nextln: => block2[%5:gpr]
    # nextln: block3:
    # nextln:     %6:gpr(reg)[late] = MovRI(S32, 1)
    # nextln:     Jump(block4)
    # nextln: => block4[%5:gpr, %7:gpr]
    # nextln: block4[%0:gpr, %1:gpr]:
    # nextln:     %3:gpr(reg)[late] = AluRRm(S32, Add) %0(tied:0)[early], %1(any)[early]
    # nextln:     %4:gpr(reg)[late] = AluRRm(S32, Sub) %1(tied:0)[early], %6(any)[early]
    # nextln:     AluRRm(S32, Cmp) %4(reg)[early], %5(any)[early]
    # nextln:     Jumpcc(E, block5, block6)
    # nextln: => block5, block6
    # nextln: block6:
    # nextln:     Jump(block4)
    # nextln: => block4[%3:gpr, %4:gpr]
    # nextln: block5:
    # nextln:     Jump(block2)
    # nextln: => block2[%3:gpr]
    # nextln: block2[%2:gpr]:
    # nextln:     Ret %2($$rax)[early]

    %0:ctrl, %1:i32 = entry
    %10:i32 = iconst 1
    %2:i32 = iconst 0
    %3:i32 = icmp eq %1, %2
    %4:ctrl, %5:ctrl = brcond %0, %3
    %13:i32 = icmp eq %11, %2
    %14:ctrl, %15:ctrl = brcond %6, %13
    %16:ctrl, %17:phisel = region %4, %14
    %6:ctrl, %7:phisel = region %5, %15
    %8:i32 = phi %7, %1, %11
    %11:i32 = isub %8, %10
    %9:i32 = phi %7, %2, %12
    %12:i32 = iadd %9, %8
    %18:i32 = phi %17, %2, %12
    return %16, %18
}

func @sum_to_n_with_stack:i32(i32) {
    # check: function `sum_to_n_with_stack`:
    # nextln:     !0 = StackSlot { size: 4, align: 4 }
    # nextln:     !1 = StackSlot { size: 4, align: 4 }
    # nextln: block0[%8:gpr($$rdi)]:
    # nextln:     Jump(block1)
    # nextln: => block1
    # nextln: block1:
    # nextln:     MovStackR(!0, S32) %8(reg)[early]
    # nextln:     %5:gpr(reg)[late] = MovRI(S32, 1)
    # nextln:     %7:gpr(reg)[late] = MovRI(S32, 0)
    # nextln:     MovStackR(!1, S32) %7(reg)[early]
    # nextln:     Jump(block2)
    # nextln: => block2
    # nextln: block2:
    # nextln:     %6:gpr(reg)[late] = MovRStack(!0, S32)
    # nextln:     AluRRm(S32, Cmp) %6(reg)[early], %7(any)[early]
    # nextln:     Jumpcc(E, block3, block4)
    # nextln: => block3, block4
    # nextln: block4:
    # nextln:     %3:gpr(reg)[late] = MovRStack(!0, S32)
    # nextln:     %2:gpr(reg)[late] = MovRStack(!1, S32)
    # nextln:     %4:gpr(reg)[late] = AluRRm(S32, Sub) %3(tied:0)[early], %5(any)[early]
    # nextln:     MovStackR(!0, S32) %4(reg)[early]
    # nextln:     %1:gpr(reg)[late] = AluRRm(S32, Add) %2(tied:0)[early], %3(any)[early]
    # nextln:     MovStackR(!1, S32) %1(reg)[early]
    # nextln:     Jump(block2)
    # nextln: => block2
    # nextln: block3:
    # nextln:     %0:gpr(reg)[late] = MovRStack(!1, S32)
    # nextln:     Ret %0($$rax)[early]

    %0:ctrl, %1:i32 = entry
    %2:ctrl, %3:phisel = region %0
    %5:ptr = stackslot 4:4
    %4:ptr = stackslot 4:4
    %6:ctrl = store.4 %2, %1, %4
    %24:i32 = iconst 1
    %7:i32 = iconst 0
    %8:ctrl = store.4 %6, %7, %5
    %13:i32 = icmp eq %12, %7
    %18:ctrl, %19:ctrl = brcond %11, %13
    %14:ctrl, %15:phisel = region %18
    %29:ctrl, %30:i32 = load.4 %14, %5
    return %29, %30
    %16:ctrl, %17:phisel = region %19
    %20:ctrl, %21:i32 = load.4 %16, %4
    %22:ctrl, %23:i32 = load.4 %20, %5
    %25:i32 = isub %21, %24
    %27:ctrl = store.4 %22, %25, %4
    %26:i32 = iadd %23, %21
    %28:ctrl = store.4 %27, %26, %5
    %9:ctrl, %10:phisel = region %8, %28
    %11:ctrl, %12:i32 = load.4 %9, %4
}
