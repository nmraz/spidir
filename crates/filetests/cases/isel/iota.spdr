# run: isel

func @iota(ptr, i64) {
    # check: function `iota`:
    # nextln:       block0[%4:gpr($$rdi), %2:gpr($$rsi)]:
    # nextln: 0000:      %6:gpr(any)[late] = MovRmS32(0)
    # nextln: 0001:      AluRRm(S64, Test) %2(reg)[early], %2(any)[early]
    # nextln: 0002:      Jumpcc(E, block1, block3)
    # nextln:       => block1, block3
    # nextln:       block1:
    # nextln: 0003:      Jump(block2)
    # nextln:       => block2
    # nextln:       block3:
    # nextln: 0004:      Jump(block4)
    # nextln:       => block4[%6:gpr]
    # nextln:       block4[%0:gpr]:
    # nextln: 0005:      %5:gpr(any)[late] = ShiftRmI(S64, Shl, 3) %0(tied:0)[early]
    # nextln: 0006:      %3:gpr(reg)[late] = AddRR(S64) %4(tied:0)[early], %5(reg)[early]
    # nextln: 0007:      %1:gpr(reg)[late] = AddRI(S64, 1) %0(tied:0)[early]
    # nextln: 0008:      MovMR(S64, [reg + 0]) %0(reg)[early], %3(reg)[early]
    # nextln: 0009:      AluRRm(S64, Cmp) %1(reg)[early], %2(any)[early]
    # nextln: 0010:      Jumpcc(E, block5, block6)
    # nextln:       => block5, block6
    # nextln:       block6:
    # nextln: 0011:      Jump(block4)
    # nextln:       => block4[%1:gpr]
    # nextln:       block5:
    # nextln: 0012:      Jump(block2)
    # nextln:       => block2
    # nextln:       block2:
    # nextln: 0013:      Ret

    %entry_ctrl:ctrl, %arr:ptr, %n:i64 = entry
    %zero:i64 = iconst 0
    %zerocmp:i32 = icmp eq %n, %zero
    %iszero:ctrl, %isnonzero:ctrl = brcond %entry_ctrl, %zerocmp
    %loopbody:ctrl, %loopphi:phisel = region %isnonzero, %looplatch
    %i:i64 = phi %loopphi, %zero, %inext
    %three:i64 = iconst 3
    %off:i64 = shl %i, %three
    %ptr:ptr = ptroff %arr, %off
    %poststore:ctrl = store.8 %loopbody, %i, %ptr
    %one:i64 = iconst 1
    %inext:i64 = iadd %i, %one
    %donecmp:i32 = icmp eq %inext, %n
    %loopdone:ctrl, %looplatch:ctrl = brcond %poststore, %donecmp
    %exit:ctrl, %exitphi:phisel = region %iszero, %loopdone
    return %exit
}
