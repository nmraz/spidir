# run: isel-regalloc[small-pic]

extfunc @sink_small(i32)
extfunc @sink_large(i32, i32, i32, i32, i32, i32)

func @call_permute_args_high_pressure(i32, i32, i32, i32, i32, i32, ptr) {
    # check: function `call_permute_args_high_pressure`:
    # nextln: clobbers: rax, rbx, rcx, rdx, rdi, rsi, r8, r9, r10, r11, r12, r13, r14, r15
    # nextln: frame: size 32, align 8
    # nextln:     spill0: 0
    # nextln:     spill1: 8
    # nextln:     spill2: 16
    # nextln:     spill3: 24
    # nextln:       block0:
    # nextln:           $$rax = $$r8
    # nextln:           $$r8 = $$r9
    # nextln:           $$r9 = $$rdi
    # nextln:           $$rdi = $$rsi
    # nextln: 0000:     $$rsi = MovRRbp { offset: 16 }
    # nextln: 0001:     $$r15 = MovRM(S32) $$rsi
    # nextln: 0002:     $$r10 = MovRM(S32) $$rsi
    # nextln: 0003:     $$r12 = MovRM(S32) $$rsi
    # nextln:           $$rbx = $$r10
    # nextln: 0004:     $$r13 = MovRM(S32) $$rsi
    # nextln: 0005:     $$r14 = MovRM(S32) $$rsi
    # nextln: 0006:     $$r10 = MovRM(S32) $$rsi
    # nextln:           $$spill3 = $$r10
    # nextln: 0007:     $$r10 = MovRM(S32) $$rsi
    # nextln:           $$spill2 = $$r10
    # nextln: 0008:     $$r10 = MovRM(S32) $$rsi
    # nextln:           $$spill1 = $$r10
    # nextln: 0009:     $$rsi = MovRM(S32) $$rsi
    # nextln:           $$spill0 = $$rsi
    # nextln:           $$rsi = $$rdx
    # nextln:           $$rdx = $$rcx
    # nextln:           $$rcx = $$rax
    # nextln: 0010:     CallRel(External(extfunc1)) $$rdi, $$rsi, $$rdx, $$rcx, $$r8, $$r9
    # nextln:           $$rdi = $$r15
    # nextln: 0011:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$rbx
    # nextln: 0012:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$r12
    # nextln: 0013:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$r13
    # nextln: 0014:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$r14
    # nextln: 0015:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$spill3
    # nextln: 0016:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$spill2
    # nextln: 0017:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$spill1
    # nextln: 0018:     CallRel(External(extfunc0)) $$rdi
    # nextln:           $$rdi = $$spill0
    # nextln: 0019:     CallRel(External(extfunc0)) $$rdi
    # nextln: 0020:     Ret

    %ent:ctrl, %a1:i32, %a2:i32, %a3:i32, %a4:i32, %a5:i32, %a6:i32, %p:ptr = entry

    # Fill a bunch of registers to increase pressure. We're using loads here because they have
    # control dependencies and won't be sunk by the scheduler.

    %c1:ctrl, %v1:i32 = load.4 %ent, %p
    %c2:ctrl, %v2:i32 = load.4 %c1, %p
    %c3:ctrl, %v3:i32 = load.4 %c2, %p
    %c4:ctrl, %v4:i32 = load.4 %c3, %p
    %c5:ctrl, %v5:i32 = load.4 %c4, %p
    %c6:ctrl, %v6:i32 = load.4 %c5, %p
    %c7:ctrl, %v7:i32 = load.4 %c6, %p
    %c8:ctrl, %v8:i32 = load.4 %c7, %p
    %c9:ctrl, %v9:i32 = load.4 %c8, %p

    # Shuffle %rdi, %rsi, %rdx, %rcx, %r8 and %r9
    %c10:ctrl = call @sink_large %c9, %a2, %a3, %a4, %a5, %a6, %a1

    # Keep our loaded values live across the call.

    %c11:ctrl = call @sink_small %c10, %v1
    %c12:ctrl = call @sink_small %c11, %v2
    %c13:ctrl = call @sink_small %c12, %v3
    %c14:ctrl = call @sink_small %c13, %v4
    %c15:ctrl = call @sink_small %c14, %v5
    %c16:ctrl = call @sink_small %c15, %v6
    %c17:ctrl = call @sink_small %c16, %v7
    %c18:ctrl = call @sink_small %c17, %v8
    %c19:ctrl = call @sink_small %c18, %v9

    return %c19
}
