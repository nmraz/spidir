# run: codegen

func @iota(ptr, i64) {
    # check: function `iota`:
    # nextln: 000000: 55                              push rbp
    # nextln: 000001: 48 89 e5                        mov rbp, rsp
    # nextln: 000004: b8 00 00 00 00                  mov eax, 0
    # nextln: 000009: 48 3b f0                        cmp rsi, rax
    # nextln: 00000c: 0f 84 30 00 00 00               je 0x42
    # nextln: 000012: b9 03 00 00 00                  mov ecx, 3
    # nextln: 000017: ba 01 00 00 00                  mov edx, 1
    # nextln: 00001c: 49 89 c0                        mov r8, rax
    # nextln: 00001f: 49 d3 e0                        shl r8, cl
    # nextln: 000022: 49 89 f9                        mov r9, rdi
    # nextln: 000025: 4d 03 c8                        add r9, r8
    # nextln: 000028: 49 89 c0                        mov r8, rax
    # nextln: 00002b: 4c 03 c2                        add r8, rdx
    # nextln: 00002e: 49 89 01                        mov qword ptr [r9], rax
    # nextln: 000031: 4c 3b c6                        cmp r8, rsi
    # nextln: 000034: 0f 84 08 00 00 00               je 0x42
    # nextln: 00003a: 4c 89 c0                        mov rax, r8
    # nextln: 00003d: e9 da ff ff ff                  jmp 0x1c
    # nextln: 000042: 5d                              pop rbp
    # nextln: 000043: c3                              ret

    %entry_ctrl:ctrl, %arr:ptr, %n:i64 = entry
    %zero:i64 = iconst 0
    %zerocmp:i32 = icmp eq %n, %zero
    %iszero:ctrl, %isnonzero:ctrl = brcond %entry_ctrl, %zerocmp
    %loopbody:ctrl, %loopphi:phisel = region %isnonzero, %looplatch
    %i:i64 = phi %loopphi, %zero, %inext
    %three:i64 = iconst 3
    %off:i64 = shl %i, %three
    %ptr:ptr = ptroff %arr, %off
    %poststore:ctrl = store.8 %loopbody, %i, %ptr
    %one:i64 = iconst 1
    %inext:i64 = iadd %i, %one
    %donecmp:i32 = icmp eq %inext, %n
    %loopdone:ctrl, %looplatch:ctrl = brcond %poststore, %donecmp
    %exit:ctrl, %exitphi:phisel = region %iszero, %loopdone
    return %exit
}
